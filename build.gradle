apply plugin: "maven-publish"

// String parameters for command line usage
ext {
	fromRepo = "http://repo1.maven.org/maven2"
	dependency = "junit:junit:4.11"
	toRepo = "testRepo"
	uploadFile = "testfile.txt"
}

configurations {
	artifactToDownload
}

// *** DOWNLOAD ***
// no plugins required since dependency resolution is in gradle-core

repositories { maven { url fromRepo } }

dependencies {
	artifactToDownload(dependency) { transitive = false }
}

// configuration of this task will resolve the dependencies,
// executing it will create the files
task download(type: Copy){
	from configurations.artifactToDownload
	into buildDir
}

// ****************


// **** UPLOAD ****
// this requires 'maven-publish' plugin

publishing {
	repositories { maven { url toRepo } }
	publications {
		myPublication(MavenPublication) {
			artifact "testfile.txt"
			groupId "de.thokari"
			version "0.5"
		}
	}
}

// task name alias
task upload { dependsOn publish }

// ****************
